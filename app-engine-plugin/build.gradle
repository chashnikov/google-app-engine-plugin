/*
 * Copyright 2000-2016 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

sourceSets.main.java.srcDirs = ['source', 'gen']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['testSource']
sourceSets.test.resources.srcDirs = ['testData']

def appEnginePluginName = 'google-app-engine'
intellij {
    pluginName = appEnginePluginName
    plugins 'maven', 'properties', 'java-i18n'
}

apply plugin: 'idea'
idea {
    module {
        generatedSourceDirs += file('gen')
    }
}

dependencies {
    compile project('jps-plugin')
    compile project('runtime')
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "${project.archivesBaseName}-tests"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

//todo: currently we need this to include 'ultimate' module to the plugin zip; it would be better to support such inclusion in 'intellij' plugin
project.afterEvaluate {
    def task = project.tasks.findByName("prepareSandbox")
    task.dependsOn project('ultimate').jar
    task.doLast {
        copy {
            from files(project('ultimate').jar)
            into "$task.destinationDir/$appEnginePluginName/lib"
        }
    }
}
